package convivit.api
import java.time.LocalDate

class Invitation {
    static belongsTo = [role: UserRole]
    // Status: pending, validated, canceled, overdue, closed
    String code
    String dni
    String email
    String kind
    String status
    Boolean overDue
    LocalDate fromDate
    LocalDate toDate
    LocalDate validatedAt
    LocalDate closedAt
    LocalDate canceledAt

    static constraints = {
      validatedAt nullable: true
      closedAt nullable: true
      canceledAt nullable: true
      overDue nullable: true, default: false
    }

    Invitation useIt() {
      def today = LocalDate.now()
      if (this.status == 'canceled') {
        throw new IllegalStateException("No se puede validar porque ya esta cancelada")
      }

      if (this.status == 'closed') {
        throw new IllegalStateException("No se puede validar porque ya esta cerrada")
      }

      if (this.status == 'validated') {
        throw new IllegalStateException("Invitacion ya utilizada")
      }

      if (this.fromDate > today) {
        throw new IllegalStateException("Invitacion no es valida para hoy")
      }

      if (this.fromDate < today) {
        setStatus('overdue')
        setClosedAt(LocalDate.now())
        return this
      }

      setStatus('validated')
      setValidatedAt(LocalDate.now())
      return this
    }

    Invitation closeIt() {
      def today = LocalDate.now()
      if (this.status == 'validated') {
        setStatus('closed')
        setClosedAt(LocalDate.now())
        def isOverdue = this.fromDate.compareTo(today) < 1
        setOverDue(isOverdue)
        return this
      } else {
        throw new IllegalStateException("Invitacion no esta validada")
      }
    }

    Invitation extendIt(LocalDate newDate) {
      def today = LocalDate.now()
      if (this.status == "pending" && this.fromDate.compareTo(today) < 0) {
        throw new IllegalStateException("La invitacion ya se venciÃ³")
      }

      if (this.status == 'canceled') {
        throw new IllegalStateException("No se puede extender porque ya esta cancelada")
      }

      setToDate(newDate)
      return this
    }

    Invitation cancelIt() {
      if (this.status == 'validated') {
        throw new IllegalStateException("No se puede cancelar una invitacion en uso")
      }

      if (this.status == 'canceled') {
        throw new IllegalStateException("No se puede cancelar una invitacion ya cancelada")
      }
      setStatus('canceled')
      setCanceledAt(LocalDate.now())
      return this
    }
}
